plugins {
    id 'com.github.johnrengelman.shadow'
}

loom {
    forge {
        mixinConfig "minepkl.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
    modImplementation "maven.modrinth:moonlight:1.20-2.16.16-forge"

    forgeRuntimeLibrary(include('org.pkl-lang:pkl-core:0.30.1'))
    forgeRuntimeLibrary(include('org.pkl-lang:pkl-parser:0.30.1'))
    forgeRuntimeLibrary(include('org.graalvm.polyglot:polyglot:25.0.0'))
    forgeRuntimeLibrary(include('org.graalvm.sdk:collections:25.0.0'))
    forgeRuntimeLibrary(include('org.graalvm.sdk:graal-sdk:25.0.0'))
    forgeRuntimeLibrary(include('org.graalvm.sdk:nativeimage:25.0.0'))
    forgeRuntimeLibrary(include('org.graalvm.sdk:word:25.0.0'))
    forgeRuntimeLibrary(include('org.graalvm.truffle:truffle-api:25.0.0'))
    forgeRuntimeLibrary(include('org.organicdesign:Paguro:3.10.3'))
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
